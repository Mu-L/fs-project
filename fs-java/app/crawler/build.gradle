apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    implementation project(':base:core')
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: lang3Version
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: pool2Version
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.11.3' // HTML Parser
    implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59' // Headless Browser
    // 版本兼容问题@see(https://curator.apache.org/zk-compatibility.html)
    implementation (group: 'org.apache.curator', name: 'curator-recipes', version: curatorVersion) { // ZooKeeper
        exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    }
    implementation group: 'org.apache.zookeeper', name: 'zookeeper', version: zookeeperVersion
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.8'
    implementation group: 'io.netty', name: 'netty-all', version: nettyVersion
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.24'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.26'
    implementation group: 'commons-cli', name: 'commons-cli', version: cliVersion
    implementation group: 'io.lettuce', name: 'lettuce-core', version: '5.1.6.RELEASE' // Redis
    implementation group: 'com.mysql', name: 'mysql-connector-j', version: mysqlVersion
    implementation group: 'org.mongodb', name: 'mongodb-driver-sync', version: mongodbVersion

    testImplementation project(':base:test')
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'com.iisquare.fs.app.crawler.CrawlerApplication'
    }
}

// 生成插件压缩文件
task zip(type: Zip, dependsOn: [clean, shadowJar]) {
    def appBaseDir = 'crawler-' + version
    into (appBaseDir) {
        from('build/libs') {
            include archivesBaseName + '-' + version + '-all.jar'
            rename { 'crawler.jar' }
        }
    }
    into (appBaseDir + '/bin') {
        from('bin') {
            include '*.*'
        }
    }
    into (appBaseDir + '/conf') {
        from('conf') {
            include '*.*'
        }
    }
    into (appBaseDir + '/docs') {
        from('docs') {
            include '*.*'
        }
    }
    into (appBaseDir + '/logs') {
        from('logs') {
            include '.gitkeep'
        }
    }
}
